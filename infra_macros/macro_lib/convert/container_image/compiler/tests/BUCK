##
## image_feature: The goal of this section is to get good coverage for all
## the syntax variants supported by the `image_feature` converter.  The unit
## test will then check that they become the right image compiler Items.
##

buck_genrule(
    name = "hello_world.tar",
    out = "hello_world.tar",
    bash = """d=`mktemp -d`; trap 'rm -rf "$d"' EXIT; """ +
           "touch \"$d\"/hello_world; " +
           "tar --owner=root --group=root -C \"$d\" -cf \"$OUT\" .",
)

# The next two targets exercise:
#  - all forms of constructing a make_dirs request
#  - specifying `stat` options, like user/group/mode

image_feature(
    name = "feature_dirs",
    make_dirs = [
        "/foo/bar",
        ("/foo/bar", "baz"),
    ],
)

# The permissions are bad, so any attempt to add this into a layer will fail.
image_feature(
    name = "feature_bad_dir",
    make_dirs = [
        {
            "path_to_make": "borf/beep",
            "into_dir": "/foo",
            "user": "uuu",
            "group": "ggg",
            "mode": "mmm",
        },
    ],
)

# Exercises:
#  - all forms of specifying a tarball to extract
image_feature(
    name = "feature_tar",
    tarballs = [
        (":hello_world.tar", "foo/borf"),
        {
            "tarball": ":hello_world.tar",
            "into_dir": "foo",
        },
    ],
)

# Exercises:
#  - all forms of specifying files to copy, including the rsync trailing-slash
#  - dependent features
#  - `copy_deps` also supports stat options
image_feature(
    name = "feature_copy_dirs_tar",
    copy_deps = [
        (":hello_world.tar", "/foo/bar/"),
        {
            "source": ":hello_world.tar",
            "dest": "/foo/bar/hello_world_again.tar",
            "group": "nobody",
        },
    ],
    features = [
        ":feature_bad_dir",
        ":feature_dirs",
        ":feature_tar",
    ],
)

##
## The following layers are the basis of an integration test for
## `image_layer`.  Unlike the `image_feature`s above, they must be actually
## be buildable, not just amenable to dependency analysis -- for this
## reason, we cannot e.g. depend on the broken `:feature_bad_dir` target.
##

image_layer(
    name = "hello_world_base",
    tarballs = [(":hello_world.tar", "//")],
)

image_layer(
    name = "parent_layer",
    copy_deps = [
        (":hello_world.tar", "/foo/bar/"),
        {
            "source": ":hello_world.tar",
            "dest": "/foo/bar/even_more_hello_world.tar",
        },
    ],
    features = [":feature_dirs"],
    parent_layer = ":hello_world_base",
)

image_layer(
    name = "child_layer",
    features = [":feature_tar"],
    make_dirs = [
        ("/foo", "borf"),  # for :feature_tar
        ("/foo", "extracted_hello"),
        {
            "path_to_make": "more_extracted_hello",
            "into_dir": "/foo",
            "user": "nobody",
            "group": "nobody",
            "mode": 0o711,
        },
    ],
    parent_layer = ":parent_layer",
    tarballs = [
        (":hello_world.tar", "/foo/extracted_hello"),
        {
            "tarball": ":hello_world.tar",
            "into_dir": "/foo/more_extracted_hello",
        },
    ],
)

##
## These few rules help test the `image_layer` `from_sendstream` feature.
##

python_binary(
    name = "save-demo-sendstream",
    srcs = ["save_demo_sendstream.py"],
    base_module = "",
    main_module = "save_demo_sendstream",
    par_style = "zip",  # testlib_demo_sendstreams needs this in @mode/opt
    deps = [
        "//tools/build/buck/infra_macros/macro_lib/convert/container_image" +
        "/btrfs_diff:testlib_demo_sendstreams",
    ],
)

# Future: it'd be neat to add `mutate_ops`, but that requires some wrangling
# with btrfs to get it to find the parent subvolume.
for op in ["create_ops"]:
    buck_genrule(
        name = op + ".sendstream",
        out = op + ".sendstream",
        bash = "$(exe :save-demo-sendstream) \"$OUT\" " + op,
    )

    image_layer(
        name = op,
        from_sendstream = ":" + op + ".sendstream",
    )
